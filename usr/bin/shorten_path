#!/usr/bin/zsh -f
#
# Shorten path according to hashed dir list ('hash -d' in Zsh),
# maintained in ~/.dotfiles/oh-my-zsh/lib/hash_dirs.
# This also replaces $HOME with ~.
#
# This is used from vimrc for ShortenPath.
#
# TODO: relative to $cdpath?!  (see also vim-flagship by tpope)

# Plain sh would be ~4x faster on startup, but with cat/sed etc similar slow/fast,
# and needs a cache file.

# Self-tests: run with '--test'. {{{
tests() {
  hash -d df=~/.dotfiles

  set -x -e
  cd ~
  [[ $(shorten_path .dotfiles/vimrc) == '~df/vimrc' ]]
  [[ $(shorten_path -a .vimrc) == '↬.vimrc' ]]

  cd .dotfiles
  [[ $(shorten_path ~/.dotfiles/vimrc) == 'vimrc' ]]
  [[ $(shorten_path vimrc) == 'vimrc' ]]
  [[ $(shorten_path ../.dotfiles/vimrc) == 'vimrc' ]]

  [[ $(shorten_path ~/.dotfiles/foo/bar) == 'foo/bar' ]]
  [[ $(shorten_path foo/bar) == 'foo/bar' ]]
  [[ $(shorten_path ../.dotfiles/foo/bar) == 'foo/bar' ]]

  # With "base" dir.
  cd ~
  [[ $(shorten_path .dotfiles/vimrc $PWD) == '~df/vimrc' ]]
  [[ $(shorten_path vimrc .dotfiles) == 'vimrc' ]]
  [[ $(shorten_path ~/.dotfiles/vimrc /) == '~df/vimrc' ]]

  cd .dotfiles
  [[ $(shorten_path ../.vimrc) == '../.vimrc' ]]
  [[ $(shorten_path ../non-existent) == '../non-existent' ]]

  set +x
  time ( for i in {1..200}; do shorten_path foo; done )
}
if [ x$1 = 'x--test' ]; then
  tests
  return
fi
# }}}

if [ -z "$1" ]; then
  echo "Argument (path) required." >&2
  exit 1
fi

if [ x"$1" = "x-a" ]; then
  annotate=1; shift
else
  annotate=0
fi

path="$1"
if [ -n "$2" ]; then
  cd "$2"
fi

test -f ~/.dotfiles/oh-my-zsh/lib/hash_dirs.zsh && \
  source ~/.dotfiles/oh-my-zsh/lib/hash_dirs.zsh
test -f ~/.dotfiles/oh-my-zsh/lib/hash_dirs.local.zsh && \
  source ~/.dotfiles/oh-my-zsh/lib/hash_dirs.local.zsh

# dir=${path:h:a}

abs_path=${path:a}
# Keep relative dirs, which are not going back to below base/$PWD.
if [[ $path = ../* ]] && [[ $abs_path != $PWD/* ]]; then
  printf $path
  return
fi

pwd_short=${(D)PWD}/
# NOTE: "(Q)" is required to remove escaping of e.g. "[" from the name.
path_short=${(Q)${(D)abs_path}#$pwd_short}
if [ "$annotate" = 1 ]; then
  # Annotate symlinks.
  path=
  for i in ${(s:/:)path_short}; do
    if [ -n "$path" ]; then
      path="$path/$i"
      printf "/"
    else
      path="$i"
    fi
    if [[ -h $~path ]]; then
      printf "↬$i"
    else
      printf $i
    fi
  done
else
  printf "$path_short"
fi
