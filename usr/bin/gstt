#!/usr/bin/env zsh
#
# Display short `git status` sorted by last modification.
#
# This is useful to get an overview about which files have been touched
# lately.
#
# Given the `-a` option it will call `git add -p` on all files (in sorted order).

while [[ $1 == -* ]] ; do
	case $1 in
		(-a) call_cmd=(git add -p) ; call_cmd_for_unknown=(git add) ;;
		(-c) call_cmd=(git checkout -p) ;;
		(--) shift ; break ;;
		(-h)
			printf 'usage: gstt [-a]\n'
			printf '  -a      "call git add -p on files".\n'
			return 0
			;;
		(*) printf "unkown option: '%s'\n" "$1" ; return 1 ;;
	esac
	shift
done

gstt() {
	for line in ${(f)"$(git status --short "$@")"}; do
		st=$line[1,2] ; fname=$line[4,-1]
		# echo -E $line | read -r st fname
		if [ -e $fname ]; then
			echo "${$(stat -c %y $fname)[1,19]} $st $fname"
		else
			# deleted
			echo "                    $st $fname"
		fi
	done | sort -r
}

if ((#call_cmd)); then
	for i in ${(f)"$(gstt "$@")"}; do
		wd_status=$(echo $i | cut -b22)
		if [[ $wd_status == ' ' ]]; then
			# skip: already in index (would result in "No changes." for "git add")
			continue
		fi
		fname=$(echo $i | cut -b24-)
		echo "--- $i ---"
		ls -l $fname
		if [[ $wd_status == '?' ]] && [[ ${#call_cmd_for_unknown} -gt 0 ]]; then
			read -q "yn?Add the file? (y/n) "
			echo
			if [[ $yn == 'y' ]]; then
				$call_cmd_for_unknown $fname
			fi
		else
			$call_cmd $fname
		fi
	done
else
	gstt
fi
